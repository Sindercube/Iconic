plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
}

def moduleDependencies(project, List<String> depNames) {
	def deps = depNames.iterator().collect { project.dependencies.project(path: ":$it", configuration: 'namedElements') }

	project.dependencies {
		deps.each {
			api it
		}
	}
}

base.archivesName = mod_id

allprojects {
	apply plugin: 'java'
	apply plugin: 'fabric-loom'

	group = maven_group
	version = mod_version

	java {
		withSourcesJar()
	}

	dependencies {
		minecraft "com.mojang:minecraft:$minecraft_version"
		mappings "net.fabricmc:yarn:$yarn_mappings:v2"
		modImplementation "net.fabricmc:fabric-loader:$loader_version"
		modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"
	}

	processResources {
		filesMatching("fabric.mod.json") {
			expand project.properties
		}
	}
}

subprojects {
	version = module_version

	base.archivesName = project.name

	dependencies {
		if (project.name != "iconic-base") api project(path: ":iconic-base", configuration: 'namedElements')
	}
}

dependencies {
	subprojects.each {
		api project(path: "${it.path}", configuration: "namedElements")
		include project(path: "${it.path}", configuration: "namedElements")
	}
}
